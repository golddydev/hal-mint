use aiken/transaction.{ScriptContext, Spend, Transaction}
use common/utils.{is_withdrawal_script_executed}
use hal_nft_mint/orders.{Cancel, Execute, OrderDatum, OrderRedeemer}
use hal_nft_mint/settings.{Settings, find_settings}
use sundae/multisig

validator {
  pub fn spend(
    datum: OrderDatum,
    redeemer: OrderRedeemer,
    ctx: ScriptContext,
  ) -> Bool {
    let ScriptContext(
      Transaction {
        reference_inputs,
        extra_signatories,
        validity_range,
        withdrawals,
        ..
      },
      purpose,
    ) = ctx
    expect Spend(_) = purpose

    when redeemer is {
      Cancel -> {
        expect owner: multisig.MultisigScript = datum.owner
        // Defer to sundae.multisig
        multisig.satisfied(
          owner,
          extra_signatories,
          validity_range,
          withdrawals,
        )
      }
      Execute -> {
        let Settings { mint_governor, .. } = find_settings(reference_inputs)
        is_withdrawal_script_executed(withdrawals, mint_governor)
      }
    }
  }
}
